plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    namespace 'org.androidaudioplugin.ports.lv2.dragonfly_reverb'
    compileSdk = libs.versions.android.compileSdk.get().toInteger()
    defaultConfig {
        applicationId "org.androidaudioplugin.ports.lv2.dragonfly_reverb"
        minSdk = libs.versions.android.minSdk.get().toInteger()
        versionCode 1
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                // https://github.com/google/prefab/blob/bccf5a6a75b67add30afbb6d4f7a7c50081d2d86/api/src/main/kotlin/com/google/prefab/api/Android.kt#L243
                arguments "-DANDROID_STL=c++_shared"

                // Enable this section if you want to debug native code using AddressSanitizer
                //arguments "-DANDROID_ARM_MODE=arm", "-DANDROID_STL=c++_shared"
                //cppFlags "-fsanitize=address -fno-omit-frame-pointer"
            }
        }
    }

    ndkVersion libs.versions.ndk.get()

    buildTypes {
        debug {
            packagingOptions {
                doNotStrip "**/*.so"
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        prefab true
    }

    externalNativeBuild {
        cmake {
            version libs.versions.cmake.get()
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    // AGP, Android Lint, and/or appcompat package is stupid and results in their own error to block our builds.
    // > androidx.appcompat.AppCompatIssueRegistry in /home/runner/.gradle/caches/transforms-2/files-2.1/2dfc7e2f9c4e61e976acb8f06b4a9014/appcompat-1.2.0/jars/lint.jar does not specify a vendor; see IssueRegistry#vendor
    // This is a suggested workaround (by AGP or Gradle)

    kotlinOptions {
        jvmTarget = '1.8'
    }
    androidResources {
        noCompress 'sample.wav'
    }
    lint {
        abortOnError false
    }
}

dependencies {
    implementation libs.aap.lv2
    implementation libs.aap.core
    implementation libs.aap.ui.compose.app
    implementation libs.aap.ui.web
    androidTestImplementation libs.aap.testing
    //  If you want to test aap-core and aap-lv2 locally, switch to these local references
    //  (along with settings.gradle.kts changes)
    /*
    implementation project(":androidaudioplugin-lv2")
    implementation project(":androidaudioplugin")
    implementation project(":androidaudioplugin-ui-compose-app")
    implementation project(":androidaudioplugin-ui-web")
    androidTestImplementation project(":androidaudioplugin-testing")
    */

    testImplementation libs.junit
    androidTestImplementation libs.test.ext.junit
}
